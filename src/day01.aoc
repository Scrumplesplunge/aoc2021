// Input: up to 2000 integers, one per line.
// Part 1: Count the number of numbers that are larger than the one before them.
// Part 2: Compute sliding window sums of size 3 and repeat part 1.
// Approach: Part 1 is trivial. For part 2, we can compare against the value
// back at the far edge of the sliding window as only the numbers at the edges
// of the window are different between two consecutive windows.

import "std/std.aoc" as std;

alias maxNumbers = 2000;
var numNumbers: int64;
var numbers: [maxNumbers]int64;

function readInput(): void {
  // Read the input into a temporary buffer on the stack.
  alias bufferSize = 16384;
  var buffer: [bufferSize]byte;
  var length = read(std.stdin, &buffer as *void, bufferSize);
  // Parse the input into the numbers array.
  if buffer[length - 1] != '\n' { std.die("newline"); }
  var i: []byte = &buffer;
  var end = i + length;
  while i != end {
    if numNumbers == maxNumbers { std.die("too many"); }
    i = std.readInt(i, &numbers[numNumbers]);
    numNumbers = numNumbers + 1;
    if i[0] != '\n' { std.die("syntax"); }
    while i[0] == '\n' { i = i + 1; }
  }
}

function part1(): int64 {
  var timesIncreased = 0;
  for var i = 1; i < numNumbers; i = i + 1 {
    if numbers[i] > numbers[i - 1] { timesIncreased = timesIncreased + 1; }
  }
  return timesIncreased;
}

function part2(): int64 {
  var timesIncreased = 0;
  if numNumbers < 3 { std.die("not enough"); }
  for var i = 3; i < numNumbers; i = i + 1 {
    // The new 3-wide window sum is larger if the newly-added number is larger
    // than the newly-removed number.
    if numbers[i] > numbers[i - 3] { timesIncreased = timesIncreased + 1; }
  }
  return timesIncreased;
}

function main(): int64 {
  readInput();
  std.printInt(part1());
  std.printInt(part2());
  return 0;
}
