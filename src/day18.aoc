// Input: Snail numbers, one per line: a snail number is a pair where each
// element is either a number or another snail number.
//
// Snail numbers can be added by forming a new pair containing the two input
// numbers. However, after doing this, the numbers must be reduced by repeatedly
// applying the following sequence of steps until neither rule applies:
//
//   * If any pair is nested inside four pairs, the left-most such pair
//     explodes: the left number in the pair is added to the number to its left
//     (if there is one), the right number is added to the number to its right
//     (if there is one), and the pair is replaced with a 0.
//   * If any regular number is 10 or greater, the left-most such number splits:
//     it is replaced with a pair consisting of [ceil(n/2), floor(n/2)].
//
// The magnitude of a snail number is three times the magnitude of its left
// element plus two times the magnitude of its right element. The magnitude of
// a regular number is the number itself.
//
// Part 1: Add up all the input snail numbers and compute the magnitude of the
// final answer.
//
// Part 2: Compute the largest magnitude for the sum of any two input snail
// numbers.
//
// Approach: Snail numbers can be represented with a tree-like data structure.
// Each node represents a pair, and each child is either a regular number or is
// the index of another node representing a nested pair. With this data
// structure, we can implement the adding and reduction rules for snail numbers
// to compute the sums.
//
// aoclang does not have a heap, so the nodes are allocated from a fixed-size
// buffer of nodes. Unallocated nodes are arranged in a free-list, with the
// parent pointer acting as the next pointer. This allows nodes to be
// efficiently allocated or freed.

import "std/std.aoc" as std;

struct Node {
  // The index of the parent node, or -1 if this node is a root.
  parent: int16;
  // If true, the corresponding field is a node index. Otherwise, it's a leaf.
  nodeA: bool;
  nodeB: bool;
  a: int16;
  b: int16;
}

alias maxNodes = 1024;
var nodes: [maxNodes]Node;
var numNodes: int64;
alias maxRoots = 100;
var roots: [maxRoots]int16;
var numRoots: int64;

// A free-list of unused nodes, via the parent pointers of the nodes.
var freeList: int16;

function initNodes(): void {
  for var i = 0; i < maxNodes; i = i + 1 {
    nodes[i].parent = (i + 1) as int16;
  }
  nodes[maxNodes - 1].parent = -1 as int16;
}

function newNode(): int16 {
  if freeList == -1 { std.die("too many nodes"); }
  var result = freeList;
  freeList = nodes[freeList].parent;
  return result;
}

function freeNode(nodeIndex: int16): void {
  var node = &nodes[nodeIndex];
  if node->nodeA { freeNode(node->a); }
  if node->nodeB { freeNode(node->b); }
  node->parent = freeList;
  freeList = nodeIndex;
}

function readInput(): void {
  alias bufferSize = 4192;
  var buffer: [bufferSize]byte;
  var length = std.read(std.stdin, &buffer, bufferSize);
  if length == 0 { std.die("empty"); }
  if buffer[length - 1] != '\n' { std.die("newline"); }
  var i: []byte = &buffer;
  var end = i + length;
  while i != end {
    if numRoots == maxRoots { std.die("too many roots"); }
    var isNode: bool;
    i = parseValue(i, -1 as int16, &roots[numRoots], &isNode);
    numRoots = numRoots + 1;
    if !isNode { std.die("not a pair"); }
    if i[0] != '\n' { std.die("newline"); }
    i = i + 1;
  }
}

function parseValue(
    i: []byte, parent: int16, value: *int16, isNode: *bool): []byte {
  *isNode = i[0] == '[';
  if *isNode {
    *value = newNode();
    numNodes = numNodes + 1;
    var node = &nodes[*value];
    node->parent = parent;
    if i[0] != '[' { std.die("expected '['"); }
    i = parseValue(i + 1, *value, &node->a, &node->nodeA);
    if i[0] != ',' { std.die("expected ','"); }
    i = parseValue(i + 1, *value, &node->b, &node->nodeB);
    if i[0] != ']' { std.die("expected ']'"); }
    return i + 1;
  } else {
    return std.readInt16(i, value);
  }
}

function cloneNode(nodeIndex: int16, parent: int16): int16 {
  var i = newNode();
  var node = &nodes[nodeIndex];
  var clone = &nodes[i];
  clone->parent = parent;
  clone->nodeA = node->nodeA;
  clone->nodeB = node->nodeB;
  clone->a = clone->nodeA ? cloneNode(node->a, i) : node->a;
  clone->b = clone->nodeB ? cloneNode(node->b, i) : node->b;
  return i;
}

function add(a: int16, b: int16): int16 {
  var i = newNode();
  var node = &nodes[i];
  node->parent = -1 as int16;
  node->nodeA = true;
  node->nodeB = true;
  node->a = a;
  nodes[a].parent = i;
  node->b = b;
  nodes[b].parent = i;
  return i;
}

// Find the left-most leaf of a node.
function leftLeaf(nodeIndex: int16): *int16 {
  var node = &nodes[nodeIndex];
  while node->nodeA { node = &nodes[node->a]; }
  return &node->a;
}

// Find the right-most leaf of a node.
function rightLeaf(nodeIndex: int16): *int16 {
  var node = &nodes[nodeIndex];
  while node->nodeB { node = &nodes[node->b]; }
  return &node->b;
}

// Find the leaf that is strictly before the given node, or null if there is no
// such leaf.
function predecessor(nodeIndex: int16): *int16 {
  var i = nodeIndex;
  // Iterate towards the root until we found an ancestor for which we are on the
  // right hand side.
  while true {
    var child = i;
    i = nodes[child].parent;
    if i == -1 { return null; }
    var node = &nodes[i];
    if node->nodeB && node->b == child { break; }
  }
  // The leaf is the right-most descendent of the left child of that ancestor.
  var ancestor = &nodes[i];
  return ancestor->nodeA ? rightLeaf(ancestor->a) : &ancestor->a;
}

// Find the leaf that is strictly after the given node, or null if there is no
// such leaf.
function successor(nodeIndex: int16): *int16 {
  var i = nodeIndex;
  // Iterate towards the root until we found an ancestor for which we are on the
  // left hand side.
  while true {
    var child = i;
    i = nodes[child].parent;
    if i == -1 { return null; }
    var node = &nodes[i];
    if node->nodeA && node->a == child { break; }
  }
  // The leaf is the left-most descendent of the right child of that ancestor.
  var ancestor = &nodes[i];
  return ancestor->nodeB ? leftLeaf(ancestor->b) : &ancestor->b;
}

function tryExplode(
    value: *int16, isNode: *bool, numParentNodes: int64): bool {
  if !*isNode { return false; }
  if numParentNodes == 4 {
    // std.puts("explode");
    // debug(*value);
    var i = *value;
    var node = &nodes[i];
    if node->nodeA || node->nodeB {
      std.die("exploded pair is not a primitive pair");
    }
    var p = predecessor(i);
    if p != null { *p = (*p + node->a) as int16; }
    var s = successor(i);
    if s != null { *s = (*s + node->b) as int16; }
    freeNode(i);
    *value = 0 as int16;
    *isNode = false;
    return true;
  } else {
    var node = &nodes[*value];
    return tryExplode(&node->a, &node->nodeA, numParentNodes + 1) ||
           tryExplode(&node->b, &node->nodeB, numParentNodes + 1);
  }
}

function trySplit(value: *int16, isNode: *bool, parent: int16): bool {
  if *isNode {
    var i = *value as int16;
    var node = &nodes[i];
    return trySplit(&node->a, &node->nodeA, i) ||
           trySplit(&node->b, &node->nodeB, i);
  } else {
    if *value < 10 { return false; }
    // std.puts("split");
    // std.printInt(*value);
    var a = (*value / 2) as int16;
    var b = (*value - a) as int16;
    *value = newNode();
    var node = &nodes[*value];
    *isNode = true;
    node->parent = parent;
    node->nodeA = false;
    node->nodeB = false;
    node->a = a;
    node->b = b;
    return true;
  }
}

function reduce(node: int16): bool {
  var isNode = true;
  return tryExplode(&node, &isNode, 0) || trySplit(&node, &isNode, -1 as int16);
}

function magnitude(value: int16, isNode: bool): int64 {
  if isNode {
    var node = &nodes[value];
    return 3 * magnitude(node->a, node->nodeA) +
           2 * magnitude(node->b, node->nodeB);
  } else {
    return value;
  }
}

function addAndReduce(a: int16, b: int16): int16 {
  var result = add(a, b);
  while reduce(result) {}
  return result;
}

function part1(): int64 {
  var total = cloneNode(roots[0], -1 as int16);
  for var i = 1; i < numRoots; i = i + 1 {
    total = addAndReduce(total, cloneNode(roots[i], -1 as int16));
  }
  var result = magnitude(total, true);
  freeNode(total);
  return result;
}

function part2(): int64 {
  var best = 0;
  for var i = 0; i < numRoots; i = i + 1 {
    for var j = 0; j < numRoots; j = j + 1 {
      if i == j { continue; }
      var left = cloneNode(roots[i], -1 as int16);
      var right = cloneNode(roots[j], -1 as int16);
      var result = addAndReduce(left, right);
      var value = magnitude(result, true);
      if value > best { best = value; }
      freeNode(result);
    }
  }
  return best;
}

function main(): int64 {
  initNodes();
  readInput();
  std.printInt(part1());
  std.printInt(part2());
}
