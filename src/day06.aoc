// Input: A comma-separated list of timers representing the reproductive cycle
// of lanternfish. At each tick, all timers decrement. Timers that go below zero
// reset to 6 and spawn a new fish with timer value 8.
// Part 1: How many timers exist after 80 steps?
// Part 2: How many timers exist after 256 steps?
// Approach: All timers with the same value can be handled as a group, so we
// only need to maintain a count of how many timers exist with the given value.
// We can maintain this using two circular buffers: one buffer holds the adult
// fish which never disappear, while the other buffer holds only the child fish
// that can mature into adults.

import "std/std.aoc" as std;

// input[i] is the number of fish with timer value i in the input.
var input: [9]int64;

function readInput(): void {
  alias bufferSize = 1024;
  var buffer: [bufferSize]byte;
  var length = read(std.stdin, &buffer as *void, bufferSize);
  if length == 0 { std.die("empty input"); }
  if buffer[length - 1] != '\n' { std.die("no final newline"); }
  var i: []byte = &buffer;
  var end = i + length;
  // Read the list of numbers.
  while i != end {
    var x: byte;
    i = std.readInt8(i, &x);
    if x > 8 { std.die("timer too large"); }
    input[x] = input[x] + 1;
    if i[0] != ',' { break; }
    i = i + 1;
  }
  if i[0] != '\n' || i + 1 != end { std.die("trailing characters"); }
}

function simulate(numDays: int64): int64 {
  // We can initially treat all the input fish as children, as their behaviour
  // is indistinguishable from the behaviour of adults with the same timer
  // values.
  var children = input;
  // Since we are treating all the initial fish as children, the buffer of
  // adults should be empty.
  var adults: [7]int64;
  for var i = 0; i < 7; i = i + 1 { adults[i] = 0; }
  var c = 0;  // The index of children that have timer 0.
  var a = 0;  // The index of adults that have timer 0.
  for var day = 0; day < numDays; day = day + 1 {
    // Children with timer 0 mature to adults.
    adults[a] = adults[a] + children[c];
    // New children are born from each adult with timer 0.
    children[c] = adults[a];
    // Advance by one day.
    c = c == 8 ? 0 : c + 1;
    a = a == 6 ? 0 : a + 1;
  }
  var total = 0;
  for var i = 0; i < 7; i = i + 1 { total = total + adults[i]; }
  for var i = 0; i < 9; i = i + 1 { total = total + children[i]; }
  return total;
}

function main(): void {
  readInput();
  std.printInt(simulate(80));
  std.printInt(simulate(256));
}
