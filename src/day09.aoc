import "std/std.aoc" as std;

alias maxSideLength = 100;

// Storage for the grid, with space for a border of 9 around all sides. The
// border simplifies the processing for each cell as we can always assume the
// presence of neighbours, and a neighbour with height 9 is equivalent to having
// no neighbour.
var grid: [maxSideLength + 2][maxSideLength + 2]byte;

// The width and height of the grid, excluding the border.
var gridWidth: int64;
var gridHeight: int64;

function readInput(): void {
  alias bufferSize = 16384;
  var buffer: [bufferSize]byte;
  var length = std.read(std.stdin, &buffer as *void, bufferSize);
  if buffer[length - 1] != '\n' { std.die("newline"); }
  var i: []byte = &buffer;
  var end = i + length;
  while i[gridWidth] != '\n' { gridWidth = gridWidth + 1; }
  if gridWidth > maxSideLength { std.die("too wide"); }
  while i != end {
    if gridHeight == maxSideLength { std.die("too tall"); }
    gridHeight = gridHeight + 1;
    var row = &grid[gridHeight];
    // Populate the border on either side of the row.
    (*row)[0] = 9 as byte;
    (*row)[gridWidth + 1] = 9 as byte;
    for var x = 1; x <= gridWidth; x = x + 1 {
      var value = i[x - 1];
      if value < '0' || '9' < value { std.die("invalid cell"); }
      (*row)[x] = (value - '0') as byte;
    }
    if i[gridWidth] != '\n' { std.die("expected line end"); }
    i = i + (gridWidth + 1);
  }
  // Populate the border above and below the grid.
  var top = &grid[0];
  var bottom = &grid[gridHeight + 1];
  for var x = 1; x <= gridWidth; x = x + 1 {
    (*top)[x] = 9 as byte;
    (*bottom)[x] = 9 as byte;
  }
}

function part1(): int64 {
  var sum = 0;
  for var y = 1; y <= gridHeight; y = y + 1 {
    for var x = 1; x <= gridWidth; x = x + 1 {
      var height = grid[y][x];
      if grid[y - 1][x] > height && grid[y + 1][x] > height &&
         grid[y][x - 1] > height && grid[y][x + 1] > height {
        sum = sum + height + 1;
      }
    }
  }
  return sum;
}

// Disjoint set node. Root nodes are their own parent.
struct Node {
  size: int64;
  parent: *void;
}

// Storage for disjoint set nodes. As with the grid, there is a border around
// the outside. The border nodes are not actually used at all, but their
// presence makes the indexing consistent with the grid.
var nodes: [maxSideLength + 2][maxSideLength + 2]Node;

// Find the representative of the set for a node.
function find(n: *Node): *Node {
  while n->parent as *Node != n {
    var next = n->parent as *Node;
    n->parent = next->parent;
    n = next;
  }
  return n;
}

// Merge the sets for two nodes.
// Precondition: a == find(a)
// Postcondition: result == find(result) == find(a) == find(b)
function merge(a: *Node, b: *Node): *Node {
  // if a != find(a) { std.die("precondition"); }
  b = find(b);
  if a == b { return a; }
  // Merge the two nodes together, making the larger node the new root.
  if a->size < b->size {
    b->size = b->size + a->size;
    a->parent = b as *void;
    return b;
  } else {
    a->size = a->size + b->size;
    b->parent = a as *void;
    return a;
  }
}

function part2(): int64 {
  var sum = 0;
  // Create a node for each cell.
  for var y = 1; y <= gridHeight; y = y + 1 {
    for var x = 1; x <= gridWidth; x = x + 1 {
      var node = &nodes[y][x];
      node->size = 1;
      node->parent = node as *void;
    }
  }
  // Merge all cells that are in the same basin.
  for var y = 1; y <= gridHeight; y = y + 1 {
    for var x = 1; x <= gridWidth; x = x + 1 {
      if grid[y][x] == 9 as byte { continue; }
      var node = find(&nodes[y][x]);
      // We only need to check up and left: down and right will be covered by
      // checking up or left from other cells.
      if grid[y - 1][x] != 9 as byte {
        node = merge(node, &nodes[y - 1][x]);
      }
      if grid[y][x - 1] != 9 as byte {
        node = merge(node, &nodes[y][x - 1]);
      }
    }
  }
  // Enumerate the top 3 basin sizes. An array with four slots is used so that
  // we can do insertion sort into the list but allow anything that flows past
  // the third slot to saturate the last cell instead.
  var basins: [4]int64;
  for var i = 0; i < 4; i = i + 1 { basins[i] = 0; }
  for var y = 1; y <= gridHeight; y = y + 1 {
    for var x = 1; x <= gridWidth; x = x + 1 {
      var root = find(&nodes[y][x]);
      if root->size == 0 { continue; }  // Already processed.
      var size = root->size;
      root->size = 0;
      // Insert the size into the basin list.
      var i = 3;
      while i > 0 && basins[i - 1] < size {
        basins[i] = basins[i - 1];
        i = i - 1;
      }
      basins[i] = size;
    }
  }
  return basins[0] * basins[1] * basins[2];
}

function main(): int64 {
  readInput();
  std.printInt(part1());
  std.printInt(part2());
}
