import "std/std.aoc" as std;

var buffer: [1024]byte;
var numbers: [200]int64;
var numNumbers: int64;
var set: [2021]byte;

function readInput(): void {
  var length: int64;
  length = read(0, &buffer as *void, 1024);
  if length <= 0 { std.die("bad"); }
  if buffer[length - 1] != '\n' { std.die ("bad"); }
  var i: []byte;
  i = &buffer;
  var end: []byte;
  end = i + length;
  while i != end {
    if numNumbers == 200 { std.die("too many"); }
    i = std.readInt(i, &numbers[numNumbers]);
    if numbers[numNumbers] > 2020 { std.die("too large"); }
    set[numbers[numNumbers]] = 1 as byte;
    numNumbers = numNumbers + 1;
    if i[0] != '\n' { std.die("bad"); }
    i = i + 1;
  }
}

function part1(): int64 {
  var i: int64;
  i = 0;
  while i < numNumbers {
    if set[2020 - numbers[i]] {
      return numbers[i] * (2020 - numbers[i]);
    }
    i = i + 1;
  }
  std.die("not found");
}

function part2(): int64 {
  var i: int64;
  i = 0;
  while i < numNumbers {
    var remaining: int64;
    remaining = 2020 - numbers[i];
    var j: int64;
    j = i + 1;
    while j < numNumbers {
      if numbers[j] > remaining {
        j = j + 1;
        continue;
      }
      var numK: int64;
      numK = remaining - numbers[j];
      if set[numK] {
        return numbers[i] * numbers[j] * numK;
      }
      j = j + 1;
    }
    i = i + 1;
  }
  std.die("not found");
}

function main(): int64 {
  readInput();
  std.printInt(part1());
  std.printInt(part2());
}
